plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

jacoco {
    toolVersion "0.8.12"
    reportsDirectory = layout.buildDirectory.dir('jacoco')
}

dependencies {
    implementation 'com.github.seancfoley:ipaddress:5.5.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'jakarta.servlet:jakarta.servlet-api:6.1.0' // For Context object (Spring Boot)
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4.2' // For JWTs
    implementation 'com.github.jnr:jnr-ffi:2.2.17'
    // Junixsocket imports :
    implementation 'com.kohlschutter.junixsocket:junixsocket-core:2.10.1'
    implementation 'com.kohlschutter.junixsocket:junixsocket-server:2.10.1'
    // Log4j 2 :
    implementation 'org.apache.logging.log4j:log4j-core:2.21.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.21.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.mockito:mockito-core:5.0.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.0.0'
    testImplementation 'org.junit-pioneer:junit-pioneer:2.2.0'
    // Sink tests :
    testImplementation 'org.postgresql:postgresql:42.2.23'
    testImplementation 'mysql:mysql-connector-java:8.0.33'
    testImplementation 'org.mariadb.jdbc:mariadb-java-client:3.0.5'
    testImplementation 'com.microsoft.sqlserver:mssql-jdbc:10.2.0.jre8'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

test {
    jvmArgs '--add-opens', 'java.base/java.util=ALL-UNNAMED'
    jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
    jvmArgs "-javaagent:${project.rootDir}/dist/agent.jar=mode=daemon-disabled"
    systemProperty 'AIK_agent_dir', "${project.rootDir}/dist"
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

task testWithCoverage(type: Test) {
    jvmArgs '--add-opens', 'java.base/java.util=ALL-UNNAMED'
    jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
    jvmArgs "-javaagent:${project.rootDir}/dist/agent.jar=mode=daemon-disabled"
    systemProperty 'AIK_agent_dir', "${project.rootDir}/dist"
    systemProperty 'AIK_INTERNAL_coverage_run', '1'
    useJUnitPlatform()

    jacoco {
        destinationFile = file("${buildDir}/jacoco/testWithCoverage.exec")
    }
}

jacocoTestReport {
    dependsOn testWithCoverage
    reports {
        xml.required = true
        csv.required = false
        html.required = false
    }
}

shadowJar {
    mergeServiceFiles()
    manifest {
    }
}